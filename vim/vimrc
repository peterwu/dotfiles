" let the fun begin

" commons {{{

const s:home = getenv('HOME')
for [vim_var, env_var, default_dir] in [
            \ ['s:vim_cache_home',  'XDG_CACHE_HOME',  '.cache'],
            \ ['s:vim_config_home', 'XDG_CONFIG_HOME', '.config'],
            \ ['s:vim_data_home',   'XDG_DATA_HOME',   '.local/share'],
            \ ['s:vim_state_home',  'XDG_STATE_HOME',  '.local/state']
            \ ]
    let s:env_var = getenv(env_var)
    let s:env_var_home = s:env_var ? s:env_var : s:home .. '/' .. default_dir
    let s:vim_var_home = s:env_var_home .. '/vim'

    call mkdir(s:vim_var_home, 'p', 0o700)

    execute 'const ' .. vim_var .. '="' .. s:vim_var_home .. '"'
endfor

function! s:highlight(group, fg='NONE', bg='NONE', style='NONE') abort
    execute 'highlight '         .. a:group
                \ .. ' ctermfg=' .. a:fg
                \ .. ' ctermbg=' .. a:bg
                \ .. ' cterm='   .. a:style
                \ .. ' guifg='   .. a:fg
                \ .. ' guibg='   .. a:bg
                \ .. ' gui='     .. a:style
endfunction

" }}}

" options {{{

" --------
" defaults
" --------
unlet! skip_defaults_vim
source $VIMRUNTIME/defaults.vim

" ---------
" work dirs
" ---------
call mkdir(s:vim_state_home .. '/swap', 'p', 0o700)
call mkdir(s:vim_state_home .. '/undo', 'p', 0o700)
call mkdir(s:vim_state_home .. '/view', 'p', 0o700)

" -------
" options
" -------
set autoindent
set autoread
set complete-=i
set completeopt=menu,menuone,noinsert,noselect
set cursorline
let &directory=s:vim_state_home .. '/swap'
set encoding=utf-8
set expandtab
set fileformats=unix,dos,mac
set foldmethod=marker
set formatoptions=tcqj
set hidden
set history=1000
set hlsearch
set ignorecase
set laststatus=2
set lazyredraw
set listchars=trail:·,tab:»·
set mouse=a
set mousemodel=popup_setpos
set nolist
set noshowmode
set notitle
set number
set path=.,,**
set pumheight=7
set relativenumber
set scrolloff=1
set sessionoptions-=options
set shiftround
set shiftwidth=4
set shortmess+=Ic
set showmatch
set sidescroll=1
set sidescrolloff=2
set smartcase
set smartindent
set smarttab
set smoothscroll
set softtabstop=0
set splitbelow
set splitright
set switchbuf=uselast
set tabstop=8
set termencoding=utf-8
set termguicolors
set textwidth=80
set timeoutlen=777
set ttimeoutlen=77
let &undodir=s:vim_state_home .. '/undo'
set undofile
let &viewdir=s:vim_state_home .. '/view'
set viewoptions=cursor,folds
set viminfo=%,<800,'10,/50,:100,h,f0
let &viminfofile=s:vim_state_home .. '/viminfo'
set virtualedit=block
set wildoptions=pum,tagfile

" }}}

" keymaps {{{

" leaders
let g:mapleader = ' '

" vimrc
nnoremap <silent> <Leader>cd <Cmd>cd     %:p:h<Bar>pwd<CR>
nnoremap <silent> <Leader>ev <Cmd>edit   $MYVIMRC<CR>
nnoremap <silent> <Leader>sv <Cmd>source $MYVIMRC<CR>

" disable arrow keys
noremap <Up>    <Nop>
noremap <Down>  <Nop>
noremap <Left>  <Nop>
noremap <Right> <Nop>

inoremap <Up>    <Nop>
inoremap <Down>  <Nop>
inoremap <Left>  <Nop>
inoremap <Right> <Nop>

" swap j/k <-> gj/gk
nnoremap <expr> j (v:count ? 'j' : 'gj')
nnoremap <expr> k (v:count ? 'k' : 'gk')
xnoremap <expr> j (v:count ? 'j' : 'gj')
xnoremap <expr> k (v:count ? 'k' : 'gk')

" unimpaired
nnoremap <silent> [<Space> O<Esc>j
nnoremap <silent> ]<Space> o<Esc>k

nnoremap <silent> [a <Cmd>previous<CR>
nnoremap <silent> ]a <Cmd>next<CR>
nnoremap <silent> [A <Cmd>first<CR>
nnoremap <silent> ]A <Cmd>last<CR>

nnoremap <silent> [b <Cmd>bprevious<CR>
nnoremap <silent> ]b <Cmd>bnext<CR>
nnoremap <silent> [B <Cmd>bfirst<CR>
nnoremap <silent> ]B <Cmd>blast<CR>

nnoremap <silent> [l <Cmd>lprevious<CR>
nnoremap <silent> ]l <Cmd>lnext<CR>
nnoremap <silent> [L <Cmd>lfirst<CR>
nnoremap <silent> ]L <Cmd>llast<CR>

nnoremap <silent> [q <Cmd>cprevious<CR>
nnoremap <silent> ]q <Cmd>cnext<CR>
nnoremap <silent> [Q <Cmd>cfirst<CR>
nnoremap <silent> ]Q <Cmd>clast<CR>

" open/close location list window
nnoremap <silent> <Leader>lo <Cmd>lopen<CR>
nnoremap <silent> <Leader>lc <Cmd>lclose<CR>
nnoremap <silent> <Leader>ll <Cmd>call <SID>toggle_location_list_window()<CR>

function! s:toggle_location_list_window() abort
    let l:nr1 = winnr('$')
    lopen
    let l:nr2 = winnr('$')
    if l:nr1 == l:nr2
        lclose
    endif
endfunction

" open/close quickfix window
nnoremap <silent> <Leader>qo <Cmd>copen<CR>
nnoremap <silent> <Leader>qc <Cmd>cclose<CR>
nnoremap <silent> <Leader>qq <Cmd>call <SID>toggle_quickfix_window()<CR>

function! s:toggle_quickfix_window() abort
    let l:nr1 = winnr('$')
    copen
    let l:nr2 = winnr('$')
    if l:nr1 == l:nr2
        cclose
    endif
endfunction

" use quickfix to show args
nnoremap <silent> <Leader>fa <Cmd>silent! call setqflist([], ' ',
            \ {
            \   'items' : argv()->copy()
            \             ->map({ -> { 'filename' : v:val->expand(':p') }}),
            \   'quickfixtextfunc': '<SID>format_quickfix_items'
            \ })<Bar>copen<CR>

" use quickfix to show buffers
nnoremap <silent> <Leader>fb <Cmd>silent! call setqflist([], ' ',
            \ {
            \   'items' : range(1, bufnr('$'))
            \             ->filter('buflisted(v:val)')
            \             ->map({ -> { 'bufnr' : v:val }}),
            \   'quickfixtextfunc': '<SID>format_quickfix_items'
            \ })<Bar>copen<CR>

" use quickfix to show oldfiles
nnoremap <silent> <Leader>fo <Cmd>silent! call setqflist([], ' ',
            \ {
            \   'items' : v:oldfiles->copy()
            \             ->map({ -> { 'filename' : v:val->expand(':p') }}),
            \   'quickfixtextfunc' : '<SID>format_quickfix_items'
            \ })<Bar>copen<CR>

function! s:format_quickfix_items(info) abort
    let l:items = getqflist({ 'id' : a:info.id, 'items' : 1 }).items
    let l:list = []
    for idx in range(a:info.start_idx - 1, a:info.end_idx - 1)
        let l:bufname = bufname(items[idx].bufnr)
        if l:bufname->empty()
            call add(l:list, "[No Name]")
        else
            call add(l:list, fnamemodify(l:bufname, ':~'))
        endif
    endfor
    return l:list
endfunction

" use tab to select from popup menu
inoremap <expr> <Tab>   (pumvisible() ? '<C-N>' : '<Tab>')
inoremap <expr> <S-Tab> (pumvisible() ? '<C-P>' : '<C-H>')

" stolen from neovim defaults
nnoremap Y     yg_
nnoremap <C-L> <Cmd>nohlsearch<Bar>diffupdate<Bar>normal! <C-L><CR>
inoremap <C-U> <C-G>u<C-U>
inoremap <C-W> <C-G>u<C-W>
xnoremap *     y/\V<C-R>"<CR>
xnoremap #     y?\V<C-R>"<CR>
nnoremap &     <Cmd>&&<CR>

" copy to clipboard
if has('clipboard')
    nnoremap <silent> <Leader>y  "+y
    xnoremap <silent> <Leader>y  "+y
    nnoremap <silent> <Leader>Y  "+yg_
    nnoremap <silent> <Leader>yy "+y_
elseif executable('wl-copy')
    vnoremap <silent> <Leader>y  <Cmd>set opfunc=CopyToClipboard<CR>g@
    nnoremap <silent> <Leader>y  <Cmd>set opfunc=CopyToClipboard<CR>g@
    nnoremap <silent> <Leader>Y  <Cmd>set opfunc=CopyToClipboard<CR>g@g_
    nnoremap <silent> <Leader>yy <Cmd>set opfunc=CopyToClipboard<CR>g@_

    function! CopyToClipboard(type, ...) abort
        let reg = '"'
        let sel_save = &selection
        let &selection = "inclusive"
        let cb_save  = &clipboard
        let reg_save = getreg(reg)
        let reg_type = getregtype(reg)
        let type = a:type
        if a:type == "char"
            silent execute 'normal! v`[o`]"'.reg.'y'
            let type = 'v'
        elseif a:type == "line"
            silent execute 'normal! `[V`]"'.reg.'y'
            let type = 'V'
        elseif a:type ==# "v" || a:type ==# "V" || a:type ==# "\<C-V>" || a:type == "block"
            let &selection = sel_save
            let ve = &virtualedit
            if !(a:0 && a:1)
                set virtualedit=
            endif
            silent execute 'normal! gv"'.reg.'y'
            let &virtualedit = ve
        elseif a:type =~ '^\d\+$'
            let type = 'v'
            silent execute 'normal! ^v'.a:type.'$h"'.reg.'y'
            if mode() ==# 'v'
                normal! v
            endif
        else
            let &selection = sel_save
            let &clipboard = cb_save
        endif

        call system('wl-copy', getreg(reg))

        call setreg(reg,reg_save,reg_type)
        let &selection = sel_save
        let &clipboard = cb_save
    endfunction
endif

" paste from clipboard
if has('clipboard')
    nnoremap <silent> <Leader>p "+p
    nnoremap <silent> <Leader>P "+P
    xnoremap <silent> <Leader>p "+p
    xnoremap <silent> <Leader>P "+P
elseif executable('wl-paste')
    nnoremap <silent> <Leader>p <Cmd>let @"=system('wl-paste')<CR>""p
    nnoremap <silent> <Leader>P <Cmd>let @"=system('wl-paste')<CR>""P
    vnoremap <silent> <Leader>p <Cmd>let @"=system('wl-paste')<CR>""p
    vnoremap <silent> <Leader>P <Cmd>let @"=system('wl-paste')<CR>""P
endif

" terminal
tnoremap <Esc> <C-\><C-N>
nnoremap <C-`> <Cmd>belowright terminal bash<CR>

" }}}

" commands {{{

" instant grep + quickfix
command! -nargs=+ -complete=file_in_path -bar Grep  cgetexpr s:grep(<f-args>)
command! -nargs=+ -complete=file_in_path -bar LGrep lgetexpr s:grep(<f-args>)

function! s:grep(...)
    let l:args = expandcmd(join(a:000, ' '))
    let l:cmd = substitute(&grepprg, '\$\*', l:args, '')
    return system(l:cmd)
endfunction

" force saving files that otherwise require sudoedit
command! -nargs=0 SudoWrite call s:sudo_write()
nnoremap <Leader>w! <Cmd>SudoWrite<CR>

function! s:sudo_write()
    silent! write !sudo tee % > /dev/null
    edit!
    echo expand('%:p') .. ' saved!'
endfunction

" }}}

" autos {{{

augroup auto_clang_format | autocmd!
    if executable('clang-format')
        autocmd FileType c,cpp,objc setlocal equalprg=clang-format
    endif
augroup END

augroup auto_save_folds | autocmd!
    autocmd BufWinLeave,BufLeave,BufWritePost ?* ++nested silent! mkview!
    autocmd BufWinEnter ?* silent! loadview
augroup END

augroup no_number_for_old_term | autocmd!
    autocmd TerminalWinOpen * setlocal nonumber norelativenumber
augroup END

augroup no_trailing_whitespaces | autocmd!
    autocmd BufWritePre * :%s/\s\+$//e
augroup END

augroup setup_quickfix | autocmd!
    autocmd QuickFixCmdPost cgetexpr cwindow
    autocmd QuickFixCmdPost lgetexpr lwindow

    autocmd WinLeave * if win_gettype() == "quickfix" | cclose | endif
    autocmd WinLeave * if win_gettype() == "loclist"  | lclose | endif

    autocmd Filetype qf
                \ setlocal norelativenumber statusline=%!DrawQfStatusLine()
augroup END

function! DrawQfStatusLine() abort
    return join ([
                \    '%#StatusVimMode#',
                \    ' ',
                \    s:get_vim_mode(),
                \    ' ',
                \    '%#StatusFileName#',
                \    ' ',
                \    '%<%F',
                \    '%=',
                \    ' ',
                \    '%#StatusPercent#',
                \    '%l/%L',
                \    ' '
                \ ], '')
endfunction

augroup show_color_column_per_line | autocmd!
    autocmd FileType * execute 'match ColorColumn'
                \ '/\%' .. (&textwidth ? &textwidth + 1 : 0) .. 'v./'
augroup END

augroup show_listchars_when_insert | autocmd!
    autocmd InsertEnter * set list
    autocmd InsertLeave * set nolist
augroup END

augroup override_colors | autocmd!
    autocmd ColorScheme *
                \ call s:highlight('ColorColumn', 'NONE',  'LightMagenta') |
                \ call s:highlight('TabLine',     'Black', 'LightGray')    |
                \ call s:highlight('TabLineFill', 'Black', 'White')        |
                \ call s:highlight('TabLineSel',  'White', 'DarkBlue', 'bold')
augroup END

augroup update_status_line | autocmd!
    autocmd BufEnter,BufWritePost,WinEnter,VimEnter *
                \ call s:highlight_status_line()
    autocmd ModeChanged,VimResized * redrawstatus!
augroup END

augroup use_as_man_pager | autocmd!
    autocmd FileType man setlocal textwidth=0 | match none
augroup END

" }}}

" status line {{{

function! s:highlight_status_line() abort
    call s:highlight_git_branch_status()

    call s:highlight('StatusBlank',      'Black',    'LightGray', 'NONE')
    call s:highlight('StatusFileName',   'Black',    'LightGray', 'bold')
    call s:highlight('StatusFileState',  'Brown',    'LightGray', 'bold')
    call s:highlight('StatusFileSize',   'Black',    'LightGray', 'NONE')
    call s:highlight('StatusFileFormat', 'Black',    'Gray',      'NONE')
    call s:highlight('StatusFileEncode', 'Black',    'Gray',      'NONE')
    call s:highlight('StatusPercent',    'Black',    'LightGray', 'NONE')
    call s:highlight('StatusWinNR',      'DarkBlue', 'Gray',      'bold')
endfunction

function! s:get_vim_mode() abort
    " :help mode()
    const l:mode_color_map = {
                \ 'N' : 'DarkBlue'    ,
                \ 'O' : 'DarkYellow'  ,
                \ 'V' : 'DarkCyan'    ,
                \ 'S' : 'DarkMagenta' ,
                \ 'I' : 'DarkGreen'   ,
                \ 'R' : 'DarkRed'     ,
                \ 'C' : 'Brown'       ,
                \ 'T' : 'Black'
                \ }

    let l:mode = mode()->toupper()

    if l:mode ==# 'N' && state() =~# '[o]'
        let l:mode = 'O'
    elseif l:mode =~# "[vV\<C-V>]"
        let l:mode = 'V'
    elseif l:mode =~# "[sS\<C-S>]"
        let l:mode = 'S'
    endif

    let l:color = l:mode_color_map[l:mode]
    call s:highlight('StatusVimMode', l:color, 'White', 'bold,inverse')
    return l:mode
endfunction

function! s:highlight_git_branch_status() abort
    if executable('git') < 1 | return | endif

    let l:git_icon = ''
    let l:git_dir = expand('%:p:h:S')
    let l:git_cmd = 'git -C ' .. git_dir .. ' status --branch --porcelain=2'
    silent! let l:git_cmd_result = system(l:git_cmd)->split('\n')

    " Line                                     Notes
    " ------------------------------------------------------------
    " # branch.oid <commit> | (initial)        Current commit.
    " # branch.head <branch> | (detached)      Current branch.
    " # branch.upstream <upstream_branch>      If upstream is set.
    " # branch.ab +<ahead> -<behind>           If upstream is set and
    "                                          the commit is present.
    " ------------------------------------------------------------

    if v:shell_error
        call s:highlight('StatusGitBranchStatus', 'Black', 'LightGray')
        let b:git_branch_status = ''
    else
        let l:git_branch = l:git_cmd_result->copy()
                    \ ->filter('v:val =~ "^# branch.head"')[0]->split()[2]
        let l:git_status = l:git_cmd_result->copy()
                    \ ->filter('v:val !~ "^# "')

        if l:git_status->empty()
            " clean branch
            let l:git_color = 'DarkGreen'
        else
            " dirty branch
            let l:git_color = 'DarkRed'
        endif

        call s:highlight('StatusGitBranchStatus', l:git_color, 'LightGray')
        let b:git_branch_status = l:git_icon .. ' ' .. l:git_branch
    endif
endfunction

function! s:get_file_size() abort
    let l:file = expand('%:p')
    let l:bytes = 0

    if l:file->len()
        let l:bytes = l:file->getfsize()
    else
        " it's a buffer
        let l:bytes = wordcount().bytes
    endif

    if l:bytes == 0 || l:bytes == -1 || l:bytes == -2
        return ''
    endif

    const l:_1K = 1024
    const l:_1M = 1024 * l:_1K
    const l:_1G = 1024 * l:_1M
    const l:_1T = 1024 * l:_1G
    const l:_1P = 1024 * l:_1T
    const l:_1E = 1024 * l:_1P

    if l:bytes < l:_1K
        return printf('%dB',   l:bytes)
    elseif l:bytes < l:_1M
        return printf('%.1fK', l:bytes/l:_1K)
    elseif l:bytes < l:_1G
        return printf('%.1fM', l:bytes/l:_1M)
    elseif l:bytes < l:_1T
        return printf('%.1fG', l:bytes/l:_1G)
    elseif l:bytes < l:_1P
        return printf('%.1fT', l:bytes/l:_1T)
    elseif l:bytes < l:_1E
        return printf('%.1fP', l:bytes/l:_1P)
    else " math.maxinteger = 2^63 -1
        return printf('%.1fE', l:bytes/l:_1E)
    endif
endfunction

function! DrawStatusLine() abort
    if g:statusline_winid != win_getid()
        return join ([
                    \    '%#StatusWinNR#',
                    \    ' ',
                    \    '%{winnr()}',
                    \    ' ',
                    \    '%#StatusFileName#',
                    \    ' ',
                    \    '%<%F',
                    \    '%=',
                    \    ' ',
                    \    '%#StatusPercent#',
                    \    '%P',
                    \    ' '
                    \ ], '')
    else
        return join ([
                    \    '%#StatusVimMode#',
                    \    ' ',
                    \    s:get_vim_mode(),
                    \    ' ',
                    \    '%#StatusBlank#',
                    \    ' ',
                    \    '%#StatusFileName#',
                    \    '%<%F',
                    \    '%#StatusBlank#',
                    \    ' ',
                    \    '%#StatusFileState#',
                    \    '%m%r%h%w%q',
                    \    '%#StatusBlank#',
                    \    ' ',
                    \    '%#StatusGitBranchStatus#',
                    \    '%{b:->get("git_branch_status", "")}',
                    \    '%#StatusBlank#',
                    \    '%=',
                    \    ' ',
                    \    '%#StatusFileSize#',
                    \    s:get_file_size(),
                    \    '%#StatusBlank#',
                    \    ' ',
                    \    '%#StatusFileFormat#',
                    \    ' ',
                    \    '%{&fileformat}',
                    \    ' | ',
                    \    '%#StatusFileEncode#',
                    \    '%{&fileencoding ?? &encoding}',
                    \    ' ',
                    \    '%#StatusBlank#',
                    \    ' ',
                    \    '%#StatusPercent#',
                    \    '%P',
                    \    ' '
                    \ ], '')
    endif
endfunction

call s:highlight_status_line()
set statusline=%!DrawStatusLine()

" }}}

" tab line {{{

set tabline=%!DrawTabLine()
function! DrawTabLine() abort
    let l:tabline = ''
    let l:selected_tab_nr = tabpagenr()
    let l:last_tab_nr = tabpagenr('$')

    for i in range(1, l:last_tab_nr)
        " select the highlighting
        if i == selected_tab_nr
            let l:tabline ..= '%#TabLineSel#'
        else
            let l:tabline ..= '%#TabLine#'
        endif

        " set the tab page number (for mouse clicks)
        let l:tabline ..= '%' .. i .. 'T'

        " the label consists of tab # and bufname of active window
        let l:buflist = tabpagebuflist(i)
        let l:winnr = tabpagewinnr(i)
        let l:buf = l:buflist[l:winnr - 1]
        let l:bufname = bufname(l:buf)->fnamemodify(':~')->pathshorten()

        let l:tabline ..= ' ' .. i .. ':' .. l:bufname .. ' '

        " if buffer is modified
        let l:buf_modified = getbufvar(l:buf, '&modified')
        if l:buf_modified | let l:tabline ..= '[+] ' | endif

        if i != l:last_tab_nr
            let l:tabline ..= '%#TabLineFill#' .. ' '
        endif
    endfor

    " after the last tab fill with TabLineFill and reset tab page nr
    let l:tabline ..= '%#TabLineFill#%T'

    return l:tabline
endfunction

" }}}

" plugins {{{

let s:package = 'plugged'
let &packpath = &packpath->substitute(&runtimepath->split(',')[0], s:vim_data_home, 'g')
let s:package_start_dir = s:vim_data_home .. '/pack/' .. s:package .. '/start'
let s:package_opt_dir   = s:vim_data_home .. '/pack/' .. s:package .. '/opt'

" paq:
" {
"    'paq_name' : { 'dir' : dir, 'url' : url }
" }
let s:paqs = {}

" Paq management functions
function! s:paq_init() abort
    call mkdir(s:package_start_dir, 'p')
    call mkdir(s:package_opt_dir,   'p')
endfunction

function! s:paq_add(name, args = {}) abort
    let l:url = 'https://github.com/' .. a:name
    let l:as = a:args->get('as', a:name->split('/')[1])

    if a:args->get('opt', v:false)
        let l:dir = s:package_opt_dir   .. '/' .. l:as
    else
        let l:dir = s:package_start_dir .. '/' .. l:as
    endif

    let s:paqs[l:as] = { 'dir' : l:dir->expand(':p:h'), 'url' : l:url }
endfunction

function! s:paq_update() abort
    for paq in s:paqs->items()
        let l:as  = paq[0]
        let l:dir = paq[1].dir
        let l:url = paq[1].url
        let l:sed_rm_ansi_esc_chars = 'sed
                    \ -e "s/[^[:print:]]\[[0-9;]*[a-zA-Z]//g"
                    \ -e "s/[[:cntrl:]]//g"'

        " check if it has been git cloned already
        if finddir('.git', l:dir)->empty()
            let l:git_clone_cmd =
                        \ 'git clone --depth=1 ' .. l:url .. ' ' .. l:dir
                        \ .. '|' .. l:sed_rm_ansi_esc_chars
            silent! let l:git_cmd_result =
                        \ system(l:git_clone_cmd)->split('\n')
        else
            let l:git_pull_cmd =
                        \ 'git -C ' .. l:dir .. ' pull --depth=1 ' .. '2>&1'
                        \ .. '|' .. l:sed_rm_ansi_esc_chars
            silent! let l:git_cmd_result =
                        \ system(l:git_pull_cmd)->split('\n')
        endif

        let l:doc_dir = l:dir .. '/doc'
        if l:doc_dir->isdirectory()
            execute "helptags " ..  l:doc_dir
        endif

        echo printf("%17s: %s", l:as, l:git_cmd_result[0])
    endfor
endfunction

function! s:paq_clean() abort
    let l:dirs = []
    for dir in [ s:package_start_dir, s:package_opt_dir ]
        let l:dirs += glob(dir .. '/*', v:true, v:true)
    endfor

    let l:idx = 0
    while l:idx < l:dirs->len()
        for dir in s:paqs->values()->map({ _, val -> val.dir })
            if l:dirs[l:idx] == dir
                let l:dirs[l:idx] = ''
                break
            endif
        endfor

        let l:idx += 1
    endwhile

    if l:dirs->filter('v:val != ""')->empty()
        redraw! | echo 'Package [' .. s:package .. '] is clean.'
    else
        let l:prompt =
                    \ "WARNING:"
                    \ .. "\nThe following folder(s) will be deleted.\n"
                    \ .. "\n" .. l:dirs->join("\n") .. "\n"
                    \ .. "\n" .. "Are you sure? [y/N] "
        if 'y' == input(l:prompt)->tolower()
            for dir in l:dirs
                call delete(dir, 'rf')
            endfor
        endif
    endif
endfunction

" -------
" plugins
" -------
" system plugins

" matchit
packadd! matchit

" netrw
let g:netrw_banner       = 0
let g:netrw_browse_split = 4
let g:netrw_dirhistmax   = 0
let g:netrw_keepdir      = 0
let g:netrw_list_hide    = '\(^\|\s\s\)\zs\.\S\+'
let g:netrw_liststyle    = 3
let g:netrw_winsize      = 29

nnoremap <silent> <F9> <Cmd>Lexplore<CR>

augroup vinegarize_netrw | autocmd!
    autocmd filetype netrw call s:vinegarize_netrw()
augroup END

function! s:vinegarize_netrw() abort
    nnoremap <buffer> <silent> <nowait> <LocalLeader>cd
                \ <Cmd>execute 'cd ' .. b:netrw_curdir<Bar>pwd<CR>
    nnoremap <buffer> <silent> <nowait> - k^
    nnoremap <buffer> <silent> <nowait> ~ <Cmd>edit ~/<CR>
endfunction

" termdebug
packadd! termdebug
let g:termdebug_config = {
            \ 'sign'   : '>>',
            \ 'wide'   : 1,
            \ 'winbar' : 0
            \ }
nnoremap <silent> <F5> <Cmd>Termdebug<CR>

" personal plugins
call s:paq_add('chrisbra/colorizer',
            \ {'as' : 'colorizer'})
call s:paq_add('tpope/vim-commentary',
            \ {'as' : 'commentary'})
call s:paq_add('tommcdo/vim-exchange',
            \ {'as' : 'exchange'})
call s:paq_add('machakann/vim-highlightedyank',
            \ {'as' : 'highlighted-yank'})
call s:paq_add('tpope/vim-repeat',
            \ {'as' : 'repeat'})
call s:paq_add('tpope/vim-surround',
            \ {'as' : 'surround'})

" auto-pairs
call s:paq_add('LunarWatcher/auto-pairs')
let g:AutoPairsCompleteOnlyOnSpace = 1

" lion
call s:paq_add('tommcdo/vim-lion', {'as' : 'lion'})
let g:lion_squeeze_spaces = 1

" lsp
call s:paq_add('yegappan/lsp', {'as' : 'lsp'})
let lsp_opts = #{autoHighlightDiags: v:true}
let lsp_servers = []

if executable('clangd')
    call add(lsp_servers, #{
                \ name: 'clangd',
                \ filetype: ['c', 'cpp'],
                \ path: 'clangd',
                \ args: ['--background-index']
                \ })
endif

if executable('pylsp')
    call add(lsp_servers, #{
                \ name: 'pylsp',
                \ filetype: 'python',
                \ path: 'pylsp',
                \ args: []
                \ })
endif

augroup lsp_setup | autocmd!
    autocmd User LspSetup call LspOptionsSet(lsp_opts)
    autocmd User LspSetup call LspAddServer(lsp_servers)
augroup END

augroup lsp_attached | autocmd!
    autocmd User LspAttached call s:set_lsp_keymaps()
augroup END

function! s:set_lsp_keymaps() abort
    nnoremap <buffer> gd <Cmd>LspGotoDefinition<CR>
    nnoremap <buffer> gp <Cmd>LspPeekDefinition<CR>
    nnoremap <buffer> gr <Cmd>LspPeekReferences<CR>
    nnoremap <buffer> gi <Cmd>LspGotoImpl<CR>
    nnoremap <buffer> gt <Cmd>LspGotoTypeDef<CR>
    nnoremap <buffer> gR <Cmd>LspRename<CR>
    nnoremap <buffer> go <Cmd>LspDocumentSymbol<CR>
    nnoremap <buffer> [g <Cmd>LspDiag prev<CR>
    nnoremap <buffer> ]g <Cmd>LspDiag next<CR>
    nnoremap <buffer> K  <Cmd>LspHover<CR>
endfunction

" rainbow
call s:paq_add('luochen1990/rainbow')
let g:rainbow_active = 1
let g:rainbow_conf = {
            \  'guifgs'    : ['DarkBlue',
            \                 'DarkYellow',
            \                 'DarkGreen',
            \                 'DarkRed',
            \                 'DarkCyan',
            \                 'DarkMagenta'],
            \  'ctermfgs'  : ['DarkBlue',
            \                 'DarkYellow',
            \                 'DarkGreen',
            \                 'DarkRed',
            \                 'DarkCyan',
            \                 'DarkMagenta'],
            \  'operators' : '_,_'
            \ }

" sneak
call s:paq_add('justinmk/vim-sneak', {'as' : 'sneak'})
let g:sneak#label = 1

nnoremap s <Plug>Sneak_s
nnoremap S <Plug>Sneak_S

nnoremap f <Plug>Sneak_f
nnoremap F <Plug>Sneak_F
nnoremap t <Plug>Sneak_t
nnoremap T <Plug>Sneak_T

" update/clean plugins
nnoremap <silent> <Leader>Qu
            \ <Cmd>call <SID>paq_init()
            \ <Bar>call <SID>paq_update()<CR>
nnoremap <silent> <Leader>Qc
            \ <Cmd>call <SID>paq_init()
            \ <Bar>call <SID>paq_clean()<CR>

" }}}

" color scheme {{{

colorscheme wildcharm

if (has('termguicolors') && &termguicolors) || has('gui_running')
    " use terminal's color scheme
    let g:terminal_ansi_colors = [
                \   '#000000',
                \   '#a60000',
                \   '#006800',
                \   '#6f5500',
                \   '#0031a9',
                \   '#721045',
                \   '#00538b',
                \   '#e1e1e1',
                \
                \   '#585858',
                \   '#972500',
                \   '#316500',
                \   '#884900',
                \   '#354fcf',
                \   '#531ab6',
                \   '#005a5f',
                \   '#ffffff'
                \ ]
endif

" }}}
