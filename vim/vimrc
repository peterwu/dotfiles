vim9script

#####################
# let the fun begin #
#####################

# commons {{{

({
    XDG_CACHE_HOME:  $'{$HOME}/.cache',
    XDG_CONFIG_HOME: $'{$HOME}/.config',
    XDG_DATA_HOME:   $'{$HOME}/.local/share',
    XDG_STATE_HOME:  $'{$HOME}/.local/state',
})->foreach((xdg, default) => {
    const dir = $'{getenv(xdg) ?? default}/vim'
    setenv(xdg->substitute('XDG', 'VIM', ''), dir)
    mkdir(dir, 'p', 0o700)
})

def Highlight(group: string, fg='NONE', bg='NONE', style='NONE'): void
    var style_dict: dict<bool>

    style->split(',')->mapnew((_, v) => {
        style_dict[v] = true
    })

    hlset([{
        name:    group,
        ctermfg: fg,
        ctermbg: bg,
        cterm:   style_dict,
        guifg:   fg,
        guibg:   bg,
        gui:     style_dict
    }])
enddef

# }}}

# options {{{

# --------
# defaults
# --------
unlet! g:skip_defaults_vim
source $VIMRUNTIME/defaults.vim

# ---------
# work dirs
# ---------
mkdir($'{$VIM_STATE_HOME}/pack', 'p', 0o700)
mkdir($'{$VIM_STATE_HOME}/swap', 'p', 0o700)
mkdir($'{$VIM_STATE_HOME}/undo', 'p', 0o700)
mkdir($'{$VIM_STATE_HOME}/view', 'p', 0o700)

# -------
# options
# -------
set autoindent
set autoread
set complete-=i
set completeopt=menu,menuone,noinsert,noselect,preinsert
set cursorline
set diffopt+=linematch:60
set directory=$VIM_STATE_HOME/swap
set encoding=utf-8
set expandtab
set fileformats=unix,dos,mac
set formatoptions=tcqj
set hidden
set history=1000
set hlsearch
set ignorecase
set laststatus=2
set lazyredraw
set listchars=trail:·,tab:»·
set mouse=a
set mousemodel=popup_setpos
set nolist
set noshowmode
set notitle
set number
set packpath^=$VIM_DATA_HOME
set path=.,,**
set pumheight=7
set relativenumber
set scrolloff=1
set sessionoptions-=options
set shiftround
set shiftwidth=4
set shortmess+=Ic
set showmatch
set showtabline=2
set sidescroll=1
set sidescrolloff=2
set smartcase
set smartindent
set smarttab
set smoothscroll
set softtabstop=0
set splitbelow
set splitright
set switchbuf=uselast
set tabstop=8
set termencoding=utf-8

if $TERM_PROGRAM !=# 'Apple_Terminal' || $TERM_PROGRAM_VERSION !=# '447.1'
    set termguicolors
endif

set textwidth=80
set timeoutlen=777
set ttimeoutlen=77
set undodir=$VIM_STATE_HOME/undo
set undofile
set updatetime=499
set viewdir=$VIM_STATE_HOME/view
set viewoptions=cursor,folds
set viminfo=%,<800,'10,/50,:100,h,f0
set viminfofile=$VIM_STATE_HOME/viminfo
set virtualedit=block
set wildoptions=pum,tagfile

# }}}

# keymaps {{{

# leaders
g:mapleader = ' '

# vimrc
nnoremap <silent> <Leader>cd <Cmd>cd      %:p:h<Bar>pwd<CR>
nnoremap <silent> <Leader>ev <Cmd>tabedit $MYVIMRC<CR>
nnoremap <silent> <Leader>sv <Cmd>source  $MYVIMRC<CR>

# disable Ctrl-Z
nnoremap <C-Z> <nop>

# disable arrow keys
noremap <Up>    <Nop>
noremap <Down>  <Nop>
noremap <Left>  <Nop>
noremap <Right> <Nop>

inoremap <Up>    <Nop>
inoremap <Down>  <Nop>
inoremap <Left>  <Nop>
inoremap <Right> <Nop>

# swap j/k <-> gj/gk
nnoremap <expr> j (v:count > 0 ? 'j' : 'gj')
nnoremap <expr> k (v:count > 0 ? 'k' : 'gk')
xnoremap <expr> j (v:count > 0 ? 'j' : 'gj')
xnoremap <expr> k (v:count > 0 ? 'k' : 'gk')

# vimdiff as mergetool
nnoremap <expr> g1 (&diff ? ':diffget LOCAL<CR>'  : 'g1')
nnoremap <expr> g2 (&diff ? ':diffget BASE<CR>'   : 'g2')
nnoremap <expr> g3 (&diff ? ':diffget REMOTE<CR>' : 'g3')

# unimpaired
nnoremap <silent> [<Space> O<Esc>j
nnoremap <silent> ]<Space> o<Esc>k

nnoremap <silent> [a <Cmd>previous<CR>
nnoremap <silent> ]a <Cmd>next<CR>
nnoremap <silent> [A <Cmd>first<CR>
nnoremap <silent> ]A <Cmd>last<CR>

nnoremap <silent> [b <Cmd>bprevious<CR>
nnoremap <silent> ]b <Cmd>bnext<CR>
nnoremap <silent> [B <Cmd>bfirst<CR>
nnoremap <silent> ]B <Cmd>blast<CR>

nnoremap <silent> [l <Cmd>lprevious<CR>
nnoremap <silent> ]l <Cmd>lnext<CR>
nnoremap <silent> [L <Cmd>lfirst<CR>
nnoremap <silent> ]L <Cmd>llast<CR>

nnoremap <silent> [q <Cmd>cprevious<CR>
nnoremap <silent> ]q <Cmd>cnext<CR>
nnoremap <silent> [Q <Cmd>cfirst<CR>
nnoremap <silent> ]Q <Cmd>clast<CR>

# open/close location list window
nnoremap <silent> <Leader>lo <Cmd>lopen<CR>
nnoremap <silent> <Leader>lc <Cmd>lclose<CR>
nnoremap <silent> <Leader>ll <ScriptCmd>qf#ToggleWindow('ll')<CR>

# open/close quickfix window
nnoremap <silent> <Leader>qo <Cmd>copen<CR>
nnoremap <silent> <Leader>qc <Cmd>cclose<CR>
nnoremap <silent> <Leader>qq <ScriptCmd>qf#ToggleWindow('qf')<CR>

# use tab to select from popup menu
inoremap <expr> <Tab>   (pumvisible() ? '<C-N>' : '<Tab>')
inoremap <expr> <S-Tab> (pumvisible() ? '<C-P>' : '<C-H>')

# stolen from neovim defaults
nnoremap Y     yg_
nnoremap <C-L> <Cmd>nohlsearch<Bar>diffupdate<Bar>normal! <C-L><CR>
inoremap <C-U> <C-G>u<C-U>
inoremap <C-W> <C-G>u<C-W>
xnoremap *     y/\V<C-R>"<CR>
xnoremap #     y?\V<C-R>"<CR>
nnoremap &     <Cmd>&&<CR>

# copy to clipboard
if has('clipboard')
    nnoremap <silent> <Leader>y  "+y
    xnoremap <silent> <Leader>y  "+y
    nnoremap <silent> <Leader>Y  "+yg_
    nnoremap <silent> <Leader>yy "+y_
else
    nnoremap <expr> <silent> <Leader>y  osc52#Yank()
    xnoremap <expr> <silent> <Leader>y  osc52#Yank()
    nnoremap <expr> <silent> <Leader>Y  osc52#Yank('g_')
    nnoremap <expr> <silent> <Leader>yy osc52#Yank('_')
endif

# paste from clipboard
if has('clipboard')
    nnoremap <silent> <Leader>p "+p
    nnoremap <silent> <Leader>P "+P
    xnoremap <silent> <Leader>p "+p
    xnoremap <silent> <Leader>P "+P
else
    nnoremap <silent> <Leader>p <ScriptCmd>osc52#Paste("p")<CR>
    nnoremap <silent> <Leader>P <ScriptCmd>osc52#Paste("P")<CR>
    xnoremap <silent> <Leader>p <ScriptCmd>osc52#Paste("p")<CR>
    xnoremap <silent> <Leader>P <ScriptCmd>osc52#Paste("P")<CR>
endif

# terminal
nnoremap <Leader>` <Cmd>botright terminal ++close bash --login<CR>

# explorers
nnoremap <Leader><Tab>   <Cmd>Lexplore<CR>
nnoremap <Leader><S-Tab> <Cmd>Lexplore!<CR>

# }}}

# commands {{{

# instant grep + quickfix
command! -nargs=+ -complete=file_in_path -bar Grep  cgetexpr qf#Grep(<f-args>)
command! -nargs=+ -complete=file_in_path -bar LGrep lgetexpr qf#Grep(<f-args>)

# force saving files that otherwise require sudoedit
command! -nargs=0 SudoWrite {
    silent! write !sudo tee % > /dev/null
    edit!
    echo $'{expand('%:p')} saved!'
}

# }}}

# autos {{{

augroup auto_save_folds | autocmd!
    autocmd BufWinLeave,BufLeave,BufWritePost ?* ++nested silent! mkview!
    autocmd BufWinEnter ?* silent! loadview
augroup END

augroup no_number_for_old_term | autocmd!
    autocmd TerminalWinOpen * setlocal nonumber norelativenumber
augroup END

augroup no_trailing_whitespaces | autocmd!
    autocmd BufWritePre * :%s/\s\+$//e
augroup END

augroup setup_quickfix | autocmd!
    autocmd QuickFixCmdPost cgetexpr cwindow
    autocmd QuickFixCmdPost lgetexpr lwindow

    autocmd Filetype qf {
        setlocal norelativenumber
        setlocal statusline=%!DrawQfStatusLine()
    }
augroup END

def g:DrawQfStatusLine(): string
    return [
        '%#StatusVimMode#',
        ' ',
        GetVimMode(),
        ' ',
        '%#StatusFileName#',
        ' ',
        '%<%F',
        '%=',
        ' ',
        '%#StatusPercent#',
        '%l/%L',
        ' '
    ]->join('')
enddef

augroup show_color_column_per_line | autocmd!
    autocmd FileType * {
        if &textwidth > 0
            matchadd('ColorColumn', $'\%{&textwidth + 1}v.')
        endif
    }
augroup END

augroup show_listchars_when_insert | autocmd!
    autocmd InsertEnter * setlocal list
    autocmd InsertLeave * setlocal nolist
augroup END

augroup override_colors | autocmd!
    autocmd ColorScheme * {
        Highlight('ColorColumn', 'NONE', 'LightMagenta')
        Highlight('CursorLine', '', '', 'NONE')
        Highlight('TabLine', 'Black', 'LightGray')
        Highlight('TabLineFill', 'Black', 'White')
        Highlight('TabLineSel', 'White', 'DarkBlue', 'bold')

        HighlightStatusLine()
    }
augroup END

augroup update_status_line | autocmd!
    autocmd BufEnter,BufWritePost,WinEnter * HighlightStatusLine()
    autocmd ModeChanged,VimResized * redrawstatus!
augroup END

augroup use_as_man_pager | autocmd!
    autocmd FileType man setlocal textwidth=0 | match none
augroup END

# }}}

# status line {{{

def HighlightStatusLine(): void
    HighlightGitBranchStatus()

    Highlight('StatusBlank',      'Black',    'LightGray', 'NONE')
    Highlight('StatusFileName',   'Black',    'LightGray', 'bold')
    Highlight('StatusFileState',  'Brown',    'LightGray', 'bold')
    Highlight('StatusFileSize',   'Black',    'LightGray', 'NONE')
    Highlight('StatusFileFormat', 'Black',    'Gray',      'NONE')
    Highlight('StatusFileEncode', 'Black',    'Gray',      'NONE')
    Highlight('StatusPercent',    'Black',    'LightGray', 'NONE')
    Highlight('StatusWinNR',      'DarkBlue', 'Gray',      'bold')
enddef

def GetVimMode(): string
    # :help mode()
    const mode_color_map = {
        N: 'DarkBlue',
        V: 'DarkCyan',
        O: 'DarkMagenta',
        S: 'DarkYellow',
        I: 'DarkGreen',
        R: 'DarkRed',
        C: 'Brown',
        T: 'Black'
    }

    var mode = mode()->toupper()

    if mode == 'N' && state() =~ '[o]'
        mode = 'O'
    elseif mode =~ "[vV\<C-V>]"
        mode = 'V'
    elseif mode =~ "[sS\<C-S>]"
        mode = 'S'
    endif

    const color = mode_color_map[mode]
    Highlight('StatusVimMode', color, 'White', 'bold,inverse')
    return mode
enddef

def HighlightGitBranchStatus(): void
    if executable('git') < 1 | return | endif

    const git_dir = expand('%:p:h:S')
    const git_cmd = $'git -C {git_dir} status --branch --porcelain=2'
    silent! const lines = system(git_cmd)->split('\n')

    # Line                                  Notes
    # ------------------------------------------------------------
    # branch.oid <commit> | (initial)       Current commit.
    # branch.head <branch> | (detached)     Current branch.
    # branch.upstream <upstream_branch>     If upstream is set.
    # branch.ab +<ahead> -<behind>          If upstream is set and
    #                                       the commit is present.
    # ------------------------------------------------------------

    if v:shell_error > 0
        Highlight('StatusGitBranchStatus', 'Black', 'LightGray')
        b:git_branch_status = ''
        return
    endif

    const branch = lines->copy()
        ->filter('v:val =~ "^# branch.head"')[0]->split()[2]
    const dirty = !lines->copy()->filter('v:val !~ "^# "')->empty()
    const color = dirty ? 'DarkRed' : 'DarkGreen'

    Highlight('StatusGitBranchStatus', color, 'LightGray')
    b:git_branch_status = $'* {branch}'
enddef

def GetFileSize(): string
    const file = expand('%:p')
    var bytes = 0

    if file->len() > 0
        bytes = file->getfsize()
    else
        # it's a buffer
        bytes = wordcount().bytes
    endif

    if bytes == 0 || bytes == -1 || bytes == -2
        return ''
    endif

    const _1K = 1024
    const _1M = 1024 * _1K
    const _1G = 1024 * _1M
    const _1T = 1024 * _1G
    const _1P = 1024 * _1T
    const _1E = 1024 * _1P

    if bytes < _1K
        return printf('%dB',   bytes)
    elseif bytes < _1M
        return printf('%.1fK', bytes / _1K)
    elseif bytes < _1G
        return printf('%.1fM', bytes / _1M)
    elseif bytes < _1T
        return printf('%.1fG', bytes / _1G)
    elseif bytes < _1P
        return printf('%.1fT', bytes / _1T)
    elseif bytes < _1E
        return printf('%.1fP', bytes / _1P)
    else # math.maxinteger = 2^63 -1
        return printf('%.1fE', bytes / _1E)
    endif
enddef

HighlightStatusLine()
set statusline=%!DrawStatusLine()
def g:DrawStatusLine(): string
    return g:statusline_winid == win_getid()
        ? DrawStatusLineActive()
        : DrawStatusLineInactive()
enddef

def DrawStatusLineInactive(): string
    return [
        '%#StatusWinNR# ', '%{winnr()}', ' ',
        '%#StatusFileName# ', '%<%F', '%= ',
        '%#StatusPercent#', '%P', ' '
    ]->join('')
enddef

def DrawStatusLineActive(): string
    return [
        '%#StatusVimMode# ', GetVimMode(), ' ',
        '%#StatusBlank# ',
        '%#StatusFileName#', '%<%F',
        '%#StatusBlank# ',
        '%#StatusFileState#', '%m%r%h%w%q',
        '%#StatusBlank# ',
        '%#StatusGitBranchStatus#', '%{b:->get("git_branch_status", "")}',
        '%#StatusBlank#', '%= ',
        '%#StatusFileSize#', GetFileSize(),
        '%#StatusBlank# ',
        '%#StatusFileFormat# ', '%{&fileformat}', ' | ',
        '%#StatusFileEncode#', '%{&fileencoding ?? &encoding}', ' ',
        '%#StatusBlank# ',
        '%#StatusPercent#', '%P', ' '
    ]->join('')
enddef

# }}}

# tab line {{{

set tabline=%!DrawTabLine()
def g:DrawTabLine(): string
    const selected_tab_nr = tabpagenr()
    const last_tab_nr = tabpagenr('$')

    const tabs = range(1, last_tab_nr)->mapnew((_, i) =>
        printf('%s%%%dT%s%s%s',
            # select the highlighting
            i == selected_tab_nr ? '%#TabLineSel#' : '%#TabLine#',
            # set the tab page number (for mouse clicks): %{i}T
            i,
            g:GetTabLabel(i),
            GetTabBuf(i)->getbufvar('&modified') ? '[+] ' : '',
            i != last_tab_nr ? '%#TabLineFill# ' : ''))

    # after the last tab fill with TabLineFill and reset tab page nr
    return tabs->join('') .. '%#TabLineFill#%T'
enddef

# the label consists of tab # and bufname of active window
def g:GetTabLabel(tabnr: number): string
    const buf = GetTabBuf(tabnr)
    const name = buf->bufname() ?? '[No Name]'

    return printf(' %d:%s ',
        tabnr,
        name->fnamemodify(':~')->pathshorten())
enddef

def GetTabBuf(tabnr: number): number
    const buflist = tabpagebuflist(tabnr)
    return buflist[tabpagewinnr(tabnr) - 1]
enddef

# }}}

# plugins {{{

# Paq management
g:paqs = []

# -------
# plugins
# -------
# system plugins

# comment
packadd! comment

# highlight-yank
packadd! hlyank

# matchit
packadd! matchit

# nohlsearch
packadd! nohlsearch

# netrw
g:netrw_banner       = 0
g:netrw_browse_split = 4
g:netrw_dirhistmax   = 0
g:netrw_keepdir      = 0
g:netrw_list_hide    = '\(^\|\s\s\)\zs\.\S\+'
g:netrw_liststyle    = 3
g:netrw_winsize      = 29

augroup vinegarize_netrw | autocmd!
    autocmd filetype netrw {
        nnoremap <buffer> <silent> ^ <Plug>NetrwBrowseUpDir()
        nnoremap <buffer> <silent> - k
        nnoremap <buffer> <silent> ~ <Cmd>execute 'Ntree' expand('~/')<CR>
    }
augroup END

# termdebug
command! -nargs=0 EnableTermDebug {
    packadd termdebug

    g:termdebug_config = { sign: '>>', wide: 1, winbar: 0 }

    nnoremap <silent> <F5> <Cmd>Termdebug<CR>
}

# 3rd party plugins
g:paqs->add(['github/copilot.vim',   {as: 'copilot'}])
g:paqs->add(['tommcdo/vim-exchange', {as: 'exchange'}])
g:paqs->add(['tpope/vim-repeat',     {as: 'repeat'}])
g:paqs->add(['tpope/vim-surround',   {as: 'surround'}])

# fzf
if executable('fzf')
    g:paqs->add(['junegunn/fzf.vim', {as: 'fzf'}])

    if executable('brew')
        const fzf_prefix = system('brew --prefix fzf')->trim()
        execute $'set runtimepath+={fzf_prefix}'
    endif

    g:fzf_vim = {
        command_prefix: 'Fzf',
        preview_window: ['hidden,right,50%', 'ctrl-y']
    }

    # key binds
    nnoremap <silent> <Leader>ff <Cmd>FzfFiles<CR>
    nnoremap <silent> <Leader>fb <Cmd>FzfBuffers<CR>
    nnoremap <silent> <Leader>fo <Cmd>FzfHistory<CR>
    nnoremap <silent> <leader>ft <Cmd>FzfTabs<CR>
    nnoremap <silent> <Leader>f: <Cmd>FzfHistory:<CR>
    nnoremap <silent> <Leader>f/ <Cmd>FzfHistory/<CR>

    command! -nargs=0 FzfTabs fzf#Tabs()
endif

# lion
g:paqs->add(['tommcdo/vim-lion', {as: 'lion'}])
g:lion_squeeze_spaces = 1

# lsp
g:paqs->add(['yegappan/lsp', {as: 'lsp'}])
const lsp_opts = {
    outlineWinSize: 31,
    popupBorder: true,
}

final lsp_servers = [
    executable('clangd') ? {
        name: 'clangd',
        filetype: ['c', 'cpp'],
        path: 'clangd',
        args: ['--background-index']
    } : {},

    executable('gopls') ? {
        name: 'gopls',
        filetype: ['go'],
        path: 'gopls'
    } : {},

    executable('neocmakelsp') ? {
        name: 'neocmakelsp',
        filetype: ['cmake'],
        path: 'neocmakelsp',
        args: ['--stdio']
    } : {},

    executable('pylsp') ? {
        name: 'pylsp',
        filetype: ['python'],
        path: 'pylsp',
        initializationOptions: {
            'pylsp': {
                'plugins': {
                    'jedi': {
                        'environment': system("pvx python find")->trim()
                    }
                }
            }
        }
    } : {},

    executable('terraform-ls') ? {
        name: 'terraform-ls',
        filetype: ['terraform'],
        path: 'terraform-ls',
        args: ['serve']
    } : {},

    executable('typescript-language-server') ? {
        name: 'typescriptlang',
        filetype: ['javascript', 'typescript'],
        path: 'typescript-language-server',
        args: ['--stdio']
    } : {}
]->filter((_, v) => !empty(v))

augroup lsp_setup | autocmd!
    autocmd User LspSetup {
        g:LspOptionsSet(lsp_opts)
        g:LspAddServer(lsp_servers)
    }
augroup END

augroup lsp_attached | autocmd!
    autocmd User LspAttached {
        lsp#SetKeymaps()

        autocmd CursorHold <buffer> silent! LspDiagCurrent

        setlocal tagfunc=lsp#lsp#TagFunc
        setlocal formatexpr=lsp#lsp#FormatExpr()
    }
augroup END

# rainbow
g:paqs->add(['luochen1990/rainbow', {}])
g:rainbow_active = 1
g:rainbow_conf = {
    guifgs: [
        'DarkBlue',
        'DarkYellow',
        'DarkGreen',
        'DarkRed',
        'DarkCyan',
        'DarkMagenta'
    ],
    ctermfgs: [
        'DarkBlue',
        'DarkYellow',
        'DarkGreen',
        'DarkRed',
        'DarkCyan',
        'DarkMagenta'
    ],
    operators: '_,_'
}

# sneak
g:paqs->add(['justinmk/vim-sneak', {as: 'sneak'}])
g:sneak#label = 1

nnoremap s <Plug>Sneak_s
nnoremap S <Plug>Sneak_S

nnoremap f <Plug>Sneak_f
nnoremap F <Plug>Sneak_F
nnoremap t <Plug>Sneak_t
nnoremap T <Plug>Sneak_T

# clean/update plugins
nnoremap <silent> <Leader>Qc <Cmd>PaqClean<CR>
nnoremap <silent> <Leader>Qu <Cmd>PaqUpdate<CR>

command! -nargs=0 PaqClean {
    paq#Init()
    paq#Clean()
}

command! -nargs=0 PaqUpdate {
    paq#Init()
    paq#Update()
}

# }}}

# color scheme {{{

set background=light
colorscheme wildcharm

# }}}

# vim:fdm=marker:fdl=0
